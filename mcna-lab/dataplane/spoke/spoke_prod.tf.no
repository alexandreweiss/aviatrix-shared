// PRD SPOKE in R1

resource "azurerm_resource_group" "azr-r1-spoke-prd-rg" {
  location = var.azure_r1_location
  name     = "azr-${var.azure_r1_location_short}-spoke-prd-rg"
}

resource "azurerm_virtual_network" "azure-spoke-prd-r1" {
  address_space       = ["10.10.1.0/24"]
  location            = var.azure_r1_location
  name                = "azr-${var.azure_r1_location_short}-spoke-prd-vn"
  resource_group_name = azurerm_resource_group.azr-r1-spoke-prd-rg.name
}

resource "azurerm_subnet" "r1-azure-spoke-prd-gw-subnet" {
  address_prefixes     = ["10.10.1.0/28"]
  name                 = "avx-gw-subnet"
  resource_group_name  = azurerm_resource_group.azr-r1-spoke-prd-rg.name
  virtual_network_name = azurerm_virtual_network.azure-spoke-prd-r1.name
}

resource "azurerm_subnet" "r1-azure-spoke-prd-hagw-subnet" {
  address_prefixes     = ["10.10.1.16/28"]
  name                 = "avx-hagw-subnet"
  resource_group_name  = azurerm_resource_group.azr-r1-spoke-prd-rg.name
  virtual_network_name = azurerm_virtual_network.azure-spoke-prd-r1.name
}

resource "azurerm_subnet" "r1-azure-spoke-prd-vm-subnet" {
  address_prefixes     = ["10.10.1.32/28"]
  name                 = "avx-vm-subnet"
  resource_group_name  = azurerm_resource_group.azr-r1-spoke-prd-rg.name
  virtual_network_name = azurerm_virtual_network.azure-spoke-prd-r1.name
}

resource "azurerm_route_table" "r1-azure-spoke-prd-vm-subnet-rt" {
  location            = var.azure_r1_location
  name                = "azr-${var.azure_r1_location_short}-spoke-prd-vm-subnet-rt"
  resource_group_name = azurerm_resource_group.azr-r1-spoke-prd-rg.name

  route {
    address_prefix = "0.0.0.0/0"
    name           = "internetDefaultBlackhole"
    next_hop_type  = "None"
  }

  lifecycle {
    ignore_changes = [
      route,
    ]
  }
}

resource "azurerm_subnet_route_table_association" "prd-subnet-vm-rt-assoc" {
  route_table_id = azurerm_route_table.r1-azure-spoke-prd-vm-subnet-rt.id
  subnet_id      = azurerm_subnet.r1-azure-spoke-prd-vm-subnet.id
}

module "we_spoke_prd" {
  source  = "terraform-aviatrix-modules/mc-spoke/aviatrix"
  version = "1.6.1"
  count   = local.features.deploy_azr_we_spoke_prd ? 1 : 0

  cloud            = "Azure"
  name             = "we-spoke-prd"
  vpc_id           = "${azurerm_virtual_network.azure-spoke-prd-r1.name}:${azurerm_resource_group.azr-r1-spoke-prd-rg.name}:${azurerm_virtual_network.azure-spoke-prd-r1.guid}"
  gw_subnet        = azurerm_subnet.r1-azure-spoke-prd-gw-subnet.address_prefixes[0]
  hagw_subnet      = azurerm_subnet.r1-azure-spoke-prd-hagw-subnet.address_prefixes[0]
  use_existing_vpc = true
  region           = var.azure_r1_location
  account          = var.azure_account
  transit_gw       = module.azure_transit_we.transit_gateway.gw_name
  //transit_gw_egress = module.azure_transit_we_egress.transit_gateway.gw_name
  //network_domain = aviatrix_segmentation_network_domain.prd_nd.domain_name
  ha_gw          = true
  single_az_ha   = false
  resource_group = azurerm_resource_group.azr-r1-spoke-prd-rg.name
  single_ip_snat = true
}

output "spoke_prd" {
  value = module.we_spoke_prd
}
